Процедура GPT_ПриСозданииНаСервере(Форма, ПараметрыРазмещения=Неопределено) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Элементы = Форма.Элементы;
	
	Если ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда 
		
		Если Элементы.Найти("GPT_ЗаполнитьОписание")<>Неопределено Тогда 
			Возврат;
		КонецЕсли;   
		
		ДобавляемыеРеквизиты	= Новый Массив;		
		GPT_ДлительнаяОперация = Новый РеквизитФормы("GPT_ДлительнаяОперация", Новый ОписаниеТипов());
		ДобавляемыеРеквизиты.Добавить(GPT_ДлительнаяОперация);
		GPT_ДлительнаяОперация2 = Новый РеквизитФормы("GPT_ДлительнаяОперация2", Новый ОписаниеТипов());
		ДобавляемыеРеквизиты.Добавить(GPT_ДлительнаяОперация2);
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

		
		Если ЭтоУНФ() ИЛИ ЭтоРозница() Тогда
			Декорация = Форма.Элементы.Вставить("GPT_ЗаполнитьОписание", Тип("ДекорацияФормы"),Элементы.ОсновныеРеквизиты,Элементы.Комментарий); 
		Иначе 
			Декорация = Форма.Элементы.Вставить("GPT_ЗаполнитьОписание", Тип("ДекорацияФормы"),Элементы.ГруппаОписания,Элементы.Описание);   
		КонецЕсли;
		
		Декорация.Вид = ВидДекорацииФормы.Надпись;
		Декорация.Гиперссылка = Истина;
		Декорация.Заголовок = "Заполнить ИИ";
		Декорация.УстановитьДействие("Нажатие", "GPT_ЗаполнитьОписание");
		
	КонецЕсли;
	
		
КонецПроцедуры   

Функция ОтправитьЗапрос(Промт) Экспорт
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	СтрокаJSON = СериализоватьЗапросВJSON(Промт);

	Соединение = ПолучитьHTTPСоединение();   
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Запрос = Новый HTTPЗапрос("/proxy/openai/v1/chat/completions",ПолучитьHTTPЗаголовки());	
	ИначеЕсли Настройки.ИспользоватьПроксиСервер Тогда
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(Настройки.АдресПроксиСервера); 
		//у прокси ищем все до /v1
		Позиция = СтрНайти(СтруктураURI.ПутьНаСервере,"v1"); Префикс = "";
		Если Позиция>0 Тогда 
			ТекстЗапроса = "/"+Лев(СтруктураURI.ПутьНаСервере,Позиция-1);
		Иначе 
			ТекстЗапроса = "/v1/chat/completions";
		КонецЕсли;
		Запрос = Новый HTTPЗапрос(ТекстЗапроса,ПолучитьHTTPЗаголовки());
	Иначе 
		Запрос = Новый HTTPЗапрос("/v1/chat/completions",ПолучитьHTTPЗаголовки());  
	КонецЕсли;
	
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
	Результат = Соединение.ОтправитьДляОбработки(Запрос); //POST
		
	Если Результат.КодСостояния=200 Тогда 
		ЧтениеJson = Новый ЧтениеJson;
		ЧтениеJson.УстановитьСтроку(Результат.ПолучитьТелоКакСтроку());
		РезСтруктура = ПрочитатьJSON(ЧтениеJson);
		
		МассивРезультатов = РезСтруктура["choices"];			
		Если МассивРезультатов <> Неопределено 
			И МассивРезультатов.Количество() 
			И ТипЗнч(МассивРезультатов[0]) = Тип("Структура")
			И МассивРезультатов[0]["message"] <> Неопределено
			И ТипЗнч(МассивРезультатов[0]["message"]) = Тип("Структура") Тогда
			Ответ = СокрЛП(МассивРезультатов[0]["message"]["content"]);	
		Конецесли;

	Иначе 
		СообщениеобОшибке = "Не удалось получить ответ на сайте. Код ошибки: "+Результат.КодСостояния +" "+ Результат.ПолучитьТелоКакСтроку();  
		ВызватьИсключение СообщениеобОшибке;
	КонецЕсли; 
	
	Возврат Ответ;
	
КонецФункции  

Функция СериализоватьЗапросВJSON(ТекстЗапроса)	
	
	Настройки = НастройкаПоУмолчанию();
	
	Сообщения = Новый Массив;
	Сообщение = Новый Структура;
	Сообщение.Вставить("role", "user"); 
	Сообщение.Вставить("content", ТекстЗапроса); 
	Сообщения.Добавить(Сообщение); 
	
	Структура = Новый Структура; 
	Структура.Вставить("model", Настройки.МодельИИ);
	Структура.Вставить("messages", Сообщения);
	
			
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,Структура);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции  


Функция ПолучитьHTTPСоединение() 
	
	Настройки = НастройкаПоУмолчанию();
	Если ОбщегоНазначения.РазделениеВключено() Тогда   
		Возврат Новый HTTPСоединение("mangelaav-oai-proxy.hf.space",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	ИначеЕсли Настройки.ИспользоватьПроксиСервер Тогда
		СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(Настройки.АдресПроксиСервера); 
		Возврат Новый HTTPСоединение(СтруктураURI.Хост,СтруктураURI.Порт,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	Иначе
		Возврат Новый HTTPСоединение("api.openai.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	КонецЕсли;
	
КонецФункции

Функция ПолучитьHTTPЗаголовки() 
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	
	Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
    Заголовки.Вставить("Cache-Control", "no-cache");
   	Заголовки.Вставить("Accept-Encoding", "identity");
   	Заголовки.Вставить("X-Gitsell-Token", Настройки.ТокенAPI_proxy);
	Заголовки.Вставить("Authorization", "Bearer " + ПолучитьТокен());
	Возврат Заголовки;      
	
КонецФункции   

Функция ПолучитьТокен()   
	
	Результат = "";	
	Настройки = НастройкаПоУмолчанию(); 
	Если ЗначениеЗаполнено(Настройки.Токен) Тогда 
		Результат = Настройки.Токен;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат) И ОбщегоНазначения.РазделениеВключено() Тогда 
		//для фреш единый ключ  
		
	КонецЕсли;
	
	Возврат Результат;    
	
КонецФункции 

Функция НастройкаПоУмолчанию() Экспорт   
	ВыборкаНастроек = Справочники.GPT_Настройка.Выбрать();
	Если ВыборкаНастроек.Следующий() Тогда
		Возврат ВыборкаНастроек.Ссылка;
	Иначе 
		НовыеНастройки = Справочники.GPT_Настройка.СоздатьЭлемент();
		НовыеНастройки.Записать();
		Возврат НовыеНастройки.Ссылка;
	КонецЕсли;
КонецФункции

// Автоматическое заполнение дополнительных свойств номенклатуры через ИИ
Функция АвтоЗаполнитьДопСвойстваНоменклатуры(ОбъектНоменклатуры) Экспорт

    Настройки = НастройкаПоУмолчанию();
    СписокСвойств = Настройки.ДополнительныеСвойстваХранилище.Получить();
    Шаблон = Настройки.ШаблонДопСвойств;
    Если НЕ ТипЗнч(СписокСвойств)=Тип("Массив") Или НЕ ЗначениеЗаполнено(Шаблон) Тогда
        Возврат Неопределено;
    КонецЕсли;

	СписокИмен = Новый Массив;
	Для Каждого ЗаполняемоеСвойство Из СписокСвойств Цикл
		Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ОбъектНоменклатуры, ЗаполняемоеСвойство) Тогда
			СписокИмен.Добавить(ЗаполняемоеСвойство.Наименование);
		КонецЕсли;
	КонецЦикла;
	
    // Формируем промт
    Наименование = ОбъектНоменклатуры.Наименование;
    СтрокаСвойств = СтрСоединить(СписокИмен, ", ");
    Промт = СтрЗаменить(Шаблон, "{Наименование}", Наименование);
    Промт = СтрЗаменить(Промт, "{Свойства}", СтрокаСвойств);

    // Отправляем запрос к ИИ
    Ответ = ОтправитьЗапрос(Промт);
    Если НЕ ЗначениеЗаполнено(Ответ) Тогда
        Возврат Неопределено;
    КонецЕсли;

    // Ожидаем JSON
    Попытка
        Соответствие = ПолучитьСоответствиеИзJSON(Ответ);
	Исключение                        
		Сообщить(Ответ);
        Возврат Новый Соответствие();
    КонецПопытки;
    
    Возврат Соответствие;

КонецФункции

Функция ПолучитьСоответствиеИзJSON(СтрокаJSON)
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Соответствие = ПрочитатьJSON(ЧтениеJSON,Истина);
	ЧтениеJSON.Закрыть();
	
	Возврат Соответствие;
КонецФункции
	
Функция ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита) Экспорт 
	
	Список = Форма.Свойства_ОписаниеДополнительныхРеквизитов;
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяРеквизита);
	
	Если ЗначениеЗаполнено(Свойство) Тогда
	    НайденныеСтроки = Список.НайтиСтроки(Новый Структура("Свойство", Свойство));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0];		
		КонецЕсли; 		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗначениеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита) Экспорт
	Перем ЗначениеРеквизита;
	
	ПолеДополнительногоРеквизитаНаФорме = ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита);
	
	Если не ПолеДополнительногоРеквизитаНаФорме = Неопределено Тогда
		Возврат Форма[ПолеДополнительногоРеквизитаНаФорме.ИмяРеквизитаЗначение];
	КонецЕсли;
	
КонецФункции

Процедура УстановитьЗначениеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита, ЗначениеРеквизита) Экспорт
	
	ПолеДополнительногоРеквизитаНаФорме = ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита);
	
	Если не ПолеДополнительногоРеквизитаНаФорме = Неопределено Тогда
		Форма[ПолеДополнительногоРеквизитаНаФорме.ИмяРеквизитаЗначение] = ЗначениеРеквизита;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоУНФ() Экспорт
	
	ИмяКонфигурации = Метаданные.Имя;
	Если ИмяКонфигурации = "УправлениеНебольшойФирмой" Тогда 
		Возврат Истина;
	иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции   

Функция ЭтоРозница() Экспорт
	
	ИмяКонфигурации = Метаданные.Имя;
	Если ИмяКонфигурации = "Розница" Тогда 
		Возврат Истина;
	иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции



	
