&НаКлиенте
Процедура GPT_ЗаполнитьОписание(Команда)  
	
	GPT_ОтправитьЗапросНаСервере();
	
	GPT_АвтоЗаполнитьДопСвойстваНаСервере();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучениеОтветаChatGPT", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ЭтаФорма.GPT_ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);	

	ОписаниеОповещения2 = Новый ОписаниеОповещения("ПолучениеОтветаДопСвойства", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ЭтаФорма.GPT_ДлительнаяОперация2, ОписаниеОповещения2, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция GPT_ОтправитьЗапросНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = Ложь; 
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	НачалоЗапроса = Настройки.НачалоЗапроса; 
	Если НЕ ЗначениеЗаполнено(НачалоЗапроса) Тогда 
		НачалоЗапроса = "Рекламное описание товара";
	КонецЕсли;
	Промт = НачалоЗапроса + " " + Объект.Наименование;
	ЭтаФорма.GPT_ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,"GPT_ОбщийМодульСервер.ОтправитьЗапрос",Промт); 
		
КонецФункции

&НаСервере
Функция GPT_АвтоЗаполнитьДопСвойстваНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = Ложь;
	ЭтаФорма.GPT_ДлительнаяОперация2 = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"GPT_ОбщийМодульСервер.АвтоЗаполнитьДопСвойстваНоменклатуры",
		Объект.Ссылка
	);
КонецФункции

&НаКлиенте
Процедура ПолучениеОтветаChatGPT(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда 
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		ИначеЕсли Результат.Статус = "Выполнено" Тогда  	
			СтруктураОтвета = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			Объект.Описание = СтруктураОтвета;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
							
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеОтветаДопСвойства(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		ИначеЕсли Результат.Статус = "Выполнено" Тогда
			Соответствие = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			ЗаполнитьДополнительныеСвойстваНаФорме(Соответствие);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеСвойстваНаФорме(Соответствие)
	
	Для Каждого Эл Из Соответствие Цикл
		Если НЕ ЗначениеЗаполнено(GPT_ОбщийМодульСервер.ПолучитьЗначениеДополнительногоРеквизитаНаФорме(ЭтотОбъект,Эл.Ключ)) Тогда 
			GPT_ОбщийМодульСервер.УстановитьЗначениеДополнительногоРеквизитаНаФорме(ЭтотОбъект,Эл.Ключ,Эл.Значение);
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

