
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВерсияКонфигурации = Метаданные.Версия;
	ИмяКонфигурации = Метаданные.Имя;
	ДляЭтойКонфигурации = Истина;
	
	// Параметр ДополнительнаяОбработкаСсылка будет уже заполнен, если внешняя обработка добавлена в базу
	
	Если Параметры.Свойство("ДополнительнаяОбработкаСсылка") И ЗначениеЗаполнено(Параметры.ДополнительнаяОбработкаСсылка) Тогда   
		ДополнительнаяОбработкаСсылка = Параметры.ДополнительнаяОбработкаСсылка;  
	КонецЕсли;
	
	ДополнительнаяОбработкаСсылка = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию("Обновление расширений из github");  //для отладки
	
	Если ЗначениеЗаполнено(ДополнительнаяОбработкаСсылка) Тогда   
		
		ХранилищеНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительнаяОбработкаСсылка, "ХранилищеНастроек");
		
		Настройки = ХранилищеНастроек.Получить();
		Если Настройки <> Неопределено И Настройки.Свойство("Расширения") Тогда
			Расширения.Загрузить(Настройки.Расширения);
		КонецЕсли;  
	Иначе 
		Элементы.Предупреждение.Видимость = Истина;
	КонецЕсли;    
	
КонецПроцедуры  

&НаКлиенте
Процедура Обновить(Команда)
	
	ПолучитьДанныеГитхаб();   
	ПолучитьТекущиеВерсии();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеГитхаб()
	
	HTTPСоединение = Новый HTTPСоединение("api.github.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL());   
	
	Для Каждого Строка Из Расширения Цикл
		
		ИсходныйURL = Строка.УРЛ;
		Массив = СтрРазделить(ИсходныйURL, "/");
		Если Массив.Количество() < 5 Тогда
			Продолжить;
		КонецЕсли;
		
		Owner = Массив[3];
		Repo = Массив[4];  
		
		//описание репозитория
		Попытка
			URL_API_Repo = "/repos/" + Owner + "/" + Repo;
			ЗапросОписание = Новый HTTPЗапрос(URL_API_Repo, ПолучитьHTTPЗаголовки(Строка.УРЛ));
			ОтветОписание = HTTPСоединение.Получить(ЗапросОписание);
			Если ОтветОписание.КодСостояния = 200 Тогда
				ДанныеРепо = ПрочитатьJSONИзСтроки(ОтветОписание.ПолучитьТелоКакСтроку());
				Если ДанныеРепо.Свойство("description") Тогда
					Строка.КраткоеОписание = ДанныеРепо.description;
				КонецЕсли;
			КонецЕсли;
		Исключение
			Сообщить("Ошибка получения описания репозитория: " + ОписаниеОшибки());
		КонецПопытки;
		
		//релизы
		URL_API = "/repos/" + Owner + "/" + Repo + "/releases";
		
		Запрос = Новый HTTPЗапрос(URL_API,ПолучитьHTTPЗаголовки(Строка.УРЛ));
		
		Ответ = HTTPСоединение.Получить(Запрос);
		
		Если Ответ.КодСостояния = 200 Тогда
			
			МассивРелизов = ПрочитатьJSONИзСтроки(Ответ.ПолучитьТелоКакСтроку(), Истина);
			
			РелизНайден = Ложь;
			Для Каждого Релиз Из МассивРелизов Цикл
				
				Если (Релиз.Получить("prerelease")<>Неопределено И Релиз.Получить("prerelease"))
					ИЛИ (Релиз.Получить("draft")<>Неопределено И Релиз.Получить("draft")) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Релиз.Получить("assets")=Неопределено Тогда
					Продолжить;
				КонецЕсли; 
				Если РелизНайден Тогда 
					Прервать;
				КонецЕсли;
				
				Строка.ДоступнаяВерсия = "";
				Строка.ОписаниеДоступнойВерсии = "";
				
				Если НЕ Строка.ДляЭтойКонфигурации Тогда 
					Строка.ДоступнаяВерсия = Релиз.Получить("tag_name");
					Строка.ОписаниеДоступнойВерсии = Релиз.Получить("body");
					РелизНайден = Истина;
				Иначе 
					
					Для Каждого ФайлИзРелиза Из Релиз.Получить("assets") Цикл
						Если НРег(ФайлИзРелиза.Получить("name")) = "1cconf.txt" Тогда
							
							Файл1cconf = СкачатьФайлГитхаб(Строка.УРЛ, ФайлИзРелиза.Получить("browser_download_url"), НРег(ФайлИзРелиза.Получить("name")));
							
							Если Файл1cconf = Неопределено Тогда
								Сообщить("Не удалось получить файл 1cconf.txt из релиза " + Релиз.Получить("tag_name"));
								Продолжить;
							КонецЕсли;
							
							Если НЕ Файл1cconf.Свойство("ДвоичныеДанные") Тогда
								Сообщить("Файл 1cconf.txt не содержит данных.");
								Продолжить;
							КонецЕсли;
							
							ТекстФайла = ПолучитьСтрокуИзДвоичныхДанных(Файл1cconf.ДвоичныеДанные);
							
							Если РелизПодходитДляКонфигурации(ТекстФайла) Тогда
								Строка.ДоступнаяВерсия = Релиз.Получить("tag_name");
								Строка.ОписаниеДоступнойВерсии = Релиз.Получить("body"); 
								РелизНайден = Истина;
								Прервать;
							КонецЕсли;
							
						КонецЕсли; 					
						
					КонецЦикла; 
					
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			Сообщить("Ошибка получения релизов для " + ИсходныйURL + ": " + Ответ.КодСостояния);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТекущиеВерсии()     
	
	Для Каждого Стр Из Расширения Цикл 
		
		Если Стр.РучнойУчетВерсий Тогда 
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Стр.ИмяОбъекта) Тогда  //заполняется при скачивании
			Продолжить;
		КонецЕсли; 
		
		//ищем в расширениях
		МассивРасширений = РасширенияКонфигурации.Получить(Новый Структура("Имя",Стр.ИмяОбъекта));
		Если МассивРасширений.Количество() > 0 Тогда  
			Стр.ТекущаяВерсия = МассивРасширений[0].Версия;
		КонецЕсли;  
		
		//ищем в доп обработках
		ДопОбработкаСсылка = Справочники.ДополнительныеОтчетыИОбработки.НайтиПоРеквизиту("ИмяОбъекта",Стр.ИмяОбъекта);  
		Если ЗначениеЗаполнено(ДопОбработкаСсылка) Тогда 
			Стр.ТекущаяВерсия = ДопОбработкаСсылка.Версия;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СкачатьФайлГитхаб(УРЛ, URLФайла, ИмяФайла)  
	
	// Попробуем определить asset_id и скачать через GitHub API
	Если Найти(URLФайла, "/releases/download/") > 0 Тогда
		
		Массив = СтрРазделить(URLФайла, "/");
		Если Массив.Количество() > 0 Тогда
			Версия = Массив[Массив.Количество() - 2];
			Файл = Массив[Массив.Количество() - 1];
		КонецЕсли;
		
		// получаем список релизов
		ИсходныйURL = УРЛ;
		МассивУРЛ = СтрРазделить(ИсходныйURL, "/");
		Если МассивУРЛ.Количество() < 5 Тогда
			Возврат Неопределено;
		КонецЕсли;
		Owner = МассивУРЛ[3];
		Repo = МассивУРЛ[4];
		
		HTTPСоединение = Новый HTTPСоединение("api.github.com",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		Запрос = Новый HTTPЗапрос("/repos/" + Owner + "/" + Repo + "/releases", ПолучитьHTTPЗаголовки(УРЛ));
		Ответ = HTTPСоединение.Получить(Запрос);
		Если Ответ.КодСостояния = 200 Тогда
			Релизы = ПрочитатьJSONИзСтроки(Ответ.ПолучитьТелоКакСтроку(),Истина);
			Для Каждого Релиз Из Релизы Цикл
				Если Релиз.Получить("tag_name") = Версия Тогда
					Если Релиз.Получить("assets")<>Неопределено Тогда
						Для Каждого Asset Из Релиз.Получить("assets") Цикл
							Если Asset.Получить("name") = Файл Тогда
								// теперь получаем asset через API
								AssetID = Asset.Получить("id");
								URLAsset = "/repos/" + Owner + "/" + Repo + "/releases/assets/" + Формат(AssetID,"ЧГ=0");
								Заголовки = ПолучитьHTTPЗаголовки(УРЛ);
								Заголовки.Вставить("Accept", "application/octet-stream");
								Запрос = Новый HTTPЗапрос(URLAsset, Заголовки);
								Ответ = HTTPСоединение.Получить(Запрос);
												
								Если Ответ.КодСостояния = 200 Тогда
									Возврат Новый Структура("ДвоичныеДанные, ИмяФайла", Ответ.ПолучитьТелоКакДвоичныеДанные(), ИмяФайла);
									
								ИначеЕсли Ответ.КодСостояния = 302 Тогда
									АдресРесурса = Ответ.Заголовки.Получить("Location"); 
									Если АдресРесурса <> Неопределено Тогда
										СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресРесурса);
										Путь = СтруктураURI.ПутьНаСервере;
										Если Лев(Путь, 1) <> "/" Тогда
											Путь = "/" + Путь;
										КонецЕсли;
										
										СоединениеПеренапр = Новый HTTPСоединение(СтруктураURI.ИмяСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL);
										Запрос = Новый HTTPЗапрос(Путь, Заголовки);
										ОтветПеренапр = СоединениеПеренапр.Получить(Запрос);
										
										Если ОтветПеренапр.КодСостояния = 200 Тогда
											Возврат Новый Структура("ДвоичныеДанные, ИмяФайла", ОтветПеренапр.ПолучитьТелоКакДвоичныеДанные(), ИмяФайла);
										Иначе
											Сообщить("GitHub API: редирект не удался. Код: " + ОтветПеренапр.КодСостояния);
										КонецЕсли;
									Иначе
										Сообщить("GitHub API: не указан Location при редиректе");
									КонецЕсли;
									
								Иначе
									Сообщить("GitHub API: не удалось получить файл (asset). Код: " + Ответ.КодСостояния);
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Сообщить("GitHub API: не удалось получить список релизов. Код: " + Ответ.КодСостояния);
		КонецЕсли;
		Возврат Неопределено;
		
	Иначе 
		//скачивание по прямой ссылке устарело
		СтруктураURIФайла = ОбщегоНазначенияКлиентСервер.СтруктураURI(URLФайла);
		ИмяСервера = СтруктураURIФайла.ИмяСервера;
		
		ЗаголовкиФайла = Новый Соответствие;
		ЗаголовкиФайла.Вставить("Cache-Control", "no-cache");    
		Если НРег(ИмяФайла) = "1cconf.txt" Тогда
			ЗаголовкиФайла.Вставить("Accept", "application/octet-stream");
		КонецЕсли;
		
		Токен = "";
		Если ЗначениеЗаполнено(УРЛ) Тогда 
			Мас = Расширения.НайтиСтроки(Новый Структура("УРЛ", УРЛ));  
			Если Мас.Количество() > 0 Тогда
				Токен = Мас[0].Токен;
			КонецЕсли;
			
			// Для приватных репозиториев — добавляем Authorization в любом случае
			Если ЗначениеЗаполнено(Токен) Тогда 
				ЗаголовкиФайла.Вставить("Authorization", "Bearer " + Токен);
			КонецЕсли;
		КонецЕсли;
		
		Соединение = Новый HTTPСоединение(ИмяСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		Путь = СтруктураURIФайла.ПутьНаСервере;
		Если Лев(Путь, 1) <> "/" Тогда
			Путь = "/" + Путь;
		КонецЕсли;
		
		Запрос = Новый HTTPЗапрос(Путь, ЗаголовкиФайла);
		Ответ = Соединение.Получить(Запрос);
		
		Файл = Неопределено;
		
		Если НЕ Ответ.КодСостояния = 200 Тогда
			АдресРесурса = Ответ.Заголовки.Получить("Location");
			Если ЗначениеЗаполнено(АдресРесурса) Тогда
				// принудительно скачиваем оттуда
				СтруктураURIПеренапр = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресРесурса);
				ПутьПеренапр = СтруктураURIПеренапр.ПутьНаСервере;
				Если Лев(ПутьПеренапр, 1) <> "/" Тогда
					ПутьПеренапр = "/" + ПутьПеренапр;
				КонецЕсли;
				
				Запрос = Новый HTTPЗапрос(ПутьПеренапр, ЗаголовкиФайла);
				СоединениеПеренапр = Новый HTTPСоединение(СтруктураURIПеренапр.ИмяСервера,,,,,, Новый ЗащищенноеСоединениеOpenSSL);
				ОтветПеренапр = СоединениеПеренапр.Получить(Запрос);
				
				Если ОтветПеренапр.КодСостояния = 200 Тогда
					Файл = Новый Структура("ДвоичныеДанные, ИмяФайла", ОтветПеренапр.ПолучитьТелоКакДвоичныеДанные(), ИмяФайла); 
					Возврат Файл;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Ответ.КодСостояния = 200 Тогда  
			Файл = Новый Структура("ДвоичныеДанные, ИмяФайла", Ответ.ПолучитьТелоКакДвоичныеДанные(), ИмяФайла); 
			
		ИначеЕсли Ответ.КодСостояния = 302 Тогда 
			АдресРесурса = Ответ.Заголовки.Получить("Location"); 
			Если АдресРесурса <> Неопределено Тогда
				СтруктураURIПеренапр = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресРесурса);
				ИмяСервераПеренапр = СтруктураURIПеренапр.ИмяСервера;
				ПутьПеренапр = СтруктураURIПеренапр.ПутьНаСервере;
				Если Лев(ПутьПеренапр, 1) <> "/" Тогда
					ПутьПеренапр = "/" + ПутьПеренапр;
				КонецЕсли;
				
				Запрос = Новый HTTPЗапрос(ПутьПеренапр, ЗаголовкиФайла);
				СоединениеПеренапр = Новый HTTPСоединение(ИмяСервераПеренапр,,,,,, Новый ЗащищенноеСоединениеOpenSSL);
				ОтветПеренапр = СоединениеПеренапр.Получить(Запрос);
				
				Если ОтветПеренапр.КодСостояния = 200 Тогда 
					Файл = Новый Структура("ДвоичныеДанные, ИмяФайла", ОтветПеренапр.ПолучитьТелоКакДвоичныеДанные(), ИмяФайла); 
				Иначе
					Сообщить("Не удалось скачать перенаправленный файл. Код ошибки: " + ОтветПеренапр.КодСостояния);
				КонецЕсли;
			Иначе
				Сообщить("Сервер не сообщил адрес ресурса (Location).");
			КонецЕсли;
			
		Иначе
			Сообщить("Не удалось скачать файл. Код ошибки: " + Ответ.КодСостояния + ". " + Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
		
		Возврат Файл;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция РелизПодходитДляКонфигурации(ТекстКонфигов)
	
	ВерсияТекущая = СтрЗаменить(ВерсияКонфигурации, ",", ".");
	
	МассивСтрок = СтрРазделить(ТекстКонфигов, Символы.ПС);
	
	Для Каждого СтрокаКонфигурации Из МассивСтрок Цикл
		
		Если Лев(СтрокаКонфигурации, 1) = "/" Или Лев(СтрокаКонфигурации, 1) = "#" Тогда
			Продолжить;
		КонецЕсли;
		
		Массив = СтрРазделить(СтрокаКонфигурации, " ",Истина);
		Если Массив.Количество() < 2 Тогда
			Продолжить;
		КонецЕсли;
		
		// Последний элемент — диапазон версий, всё остальное — имя конфигурации
		ДиапазонВерсий = Массив[Массив.Количество() - 1];
		Код = "";
		
		Для Сч = 0 По Массив.Количество() - 2 Цикл
			Код = Код + Массив[Сч];
		КонецЦикла;
		
		Если Код = ИмяКонфигурации Тогда
			
			Подходит = Истина;
			
			Если Найти(ДиапазонВерсий, "-") > 0 Тогда
				
				Границы = СтрРазделить(ДиапазонВерсий, "-");
				МинВерсия = Границы[0];
				МаксВерсия = Границы[1]; 				
				
				Если МинВерсия <> "" Тогда
					Подходит = Подходит И (ВерсияТекущая >= МинВерсия ИЛИ НЕ ЗначениеЗаполнено(МинВерсия));
				КонецЕсли;
				
				Если МаксВерсия <> "" Тогда
					Подходит = Подходит И (ВерсияТекущая <= МаксВерсия ИЛИ НЕ ЗначениеЗаполнено(МаксВерсия));
				КонецЕсли;
				
				Если Подходит Тогда
					Возврат Истина;
				КонецЕсли;
				
			иначе
				Возврат Подходит; 
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПолучитьHTTPЗаголовки(УРЛ="") 
	
	Заголовки = Новый Соответствие;    
	Заголовки.Вставить("Content-Type","application/json;charset=utf-8");
	Заголовки.Вставить("Cache-Control","no-cache");
	Заголовки.Вставить("Keep-Alive","timeout=30, max=100;");
	Заголовки.Вставить("Connection","keep-alive");
	Заголовки.Вставить("User-Agent", "1C-Client");   
	
	Если ЗначениеЗаполнено(УРЛ) Тогда 
		Мас = Расширения.НайтиСтроки(Новый Структура("УРЛ",УРЛ)); Токен = "";  
		Если Мас.Количество()>0 Тогда
			Токен = Мас[0].Токен;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Токен) Тогда 
			Заголовки.Вставить("Authorization", "Bearer "+Токен);
		КонецЕсли;     
	КонецЕсли;
	
	Если Заголовки.Получить("Authorization") = Неопределено Тогда                                                                  
		//снимаем ограничение на 50 запросов в час
		ТокенДляПубличныхРепо = "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";
		Заголовки.Вставить("Authorization", "Bearer "+ТокенДляПубличныхРепо); 
	КонецЕсли;
	
	
	Возврат Заголовки;  
	
КонецФункции

&НаКлиенте
Процедура Добавить(Команда) 
	
	Если РепозиторийПриватный(УРЛ) Тогда                                            
		ДопПараметры = Новый Структура("УРЛ, ДляЭтойКонфигурации", УРЛ, ДляЭтойКонфигурации);
		ПоказатьВводЗначения(Новый ОписаниеОповещения("ПослеВводаТокена",ЭтотОбъект,ДопПараметры),,"Репозиторий приватный, введите токен доступа",Новый ОписаниеТипов("Строка"));
		Возврат;
	КонецЕсли;
	
	Мас = Расширения.НайтиСтроки(Новый Структура("УРЛ",УРЛ));
	Если Мас.Количество()>0 Тогда 
		Сообщить("Эта ссылка уже добавлена"); 
		Возврат;
	КонецЕсли;
	
	НовСтр = Расширения.Добавить();
	НовСтр.УРЛ = УРЛ; 
	НовСтр.ДляЭтойКонфигурации = ДляЭтойКонфигурации; 
	НовСтр.Скачать = "Скачать";
	НовСтр.Установить = "Установить";
	Если НовСтр.УРЛ = "https://github.com/msrv-tech/1c-extension-installer"  Тогда
		НовСтр.ИмяОбъекта = "ОбновлениеРасширенийИзGithub";//для обновления самого себя
	КонецЕсли;
	
	УРЛ = "";
	
	Обновить(Неопределено);
	
КонецПроцедуры  

&НаКлиенте
Процедура ПослеВводаТокена(Результат, ДопПараметры) Экспорт 
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		
		Если РепозиторийПриватный(УРЛ, Результат) Тогда 
			
			Мас = Расширения.НайтиСтроки(Новый Структура("УРЛ",УРЛ));
			Если Мас.Количество()>0 Тогда 
				Сообщить("Эта ссылка уже добавлена"); 
				Возврат;
			КонецЕсли;
			
			НовСтр = Расширения.Добавить();
			НовСтр.УРЛ = УРЛ; 
			НовСтр.ДляЭтойКонфигурации = ДопПараметры.ДляЭтойКонфигурации; 
			НовСтр.Скачать = "Скачать"; 
			НовСтр.Установить = "Установить";
			НовСтр.Токен = Результат; 
			НовСтр.Приватное = Истина;
			
			УРЛ = "";
			Обновить(Неопределено); 
			
		Иначе 
			Сообщить("Не удалось подключиться к репозиторию!");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Функция РепозиторийПриватный(УРЛ, Токен = Неопределено)  
	
	Массив = СтрРазделить(УРЛ, "/");
	Если Массив.Количество() < 5 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Owner = Массив[3];
	Repo = Массив[4];
	URL_API = "/repos/" + Owner + "/" + Repo;
	
	Заголовки = Новый Соответствие;    
	Заголовки.Вставить("Content-Type","application/json;charset=utf-8");
	Заголовки.Вставить("Cache-Control","no-cache");
	Заголовки.Вставить("Keep-Alive","timeout=30, max=100;");
	Заголовки.Вставить("Connection","keep-alive");
	Заголовки.Вставить("User-Agent", "1C-Client");  	
	Если ЗначениеЗаполнено(Токен) Тогда 
		Заголовки.Вставить("Authorization", "Bearer "+Токен);
	КонецЕсли;
	
	Запрос = Новый HTTPЗапрос(URL_API, Заголовки);
	Соединение = Новый HTTPСоединение("api.github.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда
		Данные = ПрочитатьJSONИзСтроки(Ответ.ПолучитьТелоКакСтроку());
		Если Данные.Свойство("private") Тогда
			Возврат Данные.private;
		КонецЕсли; 
		
	ИначеЕсли Токен = Неопределено И Ответ.КодСостояния = 404 Тогда 
		//возможно репо приватный
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура РасширенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Расширения.ТекущиеДанные;
	Если ТекДанные=Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "РасширенияСкачать" И ТекДанные.Скачать = "Скачать" Тогда
		СкачатьРасширение(Элементы.Расширения.ТекущаяСтрока);
	ИначеЕсли Поле.Имя = "РасширенияУстановить" И ТекДанные.Установить = "Установить" Тогда   
		//обновление установщика 
		Если ТекДанные.УРЛ = "https://github.com/msrv-tech/1c-extension-installer"  Тогда
			ОбновлениеУстановщика = Истина;   
		КонецЕсли; 
		
		УстановитьРасширение(Элементы.Расширения.ТекущаяСтрока); 

		Если ОбновлениеУстановщика Тогда 
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура СкачатьРасширение(ТекущаяСтрока)  
	
	СтрокаРасширений = Расширения.НайтиПоИдентификатору(ТекущаяСтрока);
	
	АдресХранилища = СкачатьРасширениеСервер(СтрокаРасширений.УРЛ, СтрокаРасширений.ДоступнаяВерсия);
	
	Если АдресХранилища = Неопределено Тогда	
		Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если МассивФайлов.Количество()=1 Тогда 
		Для Каждого Эл Из МассивФайлов Цикл
			ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
			ДиалогВыбораФайла.ПолноеИмяФайла = Эл.ИмяФайла;
			ДиалогВыбораФайла.Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
			АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
			ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПриЗакрытииВыбораФайла", ЭтотОбъект, АдресДД);
			ДиалогВыбораФайла.Показать(ОписаниеОповещения);
		КонецЦикла; 
		
	Иначе 
		//форма выбора установок                                                                
		ДопПараметры = Новый Структура("ТекущаяСтрока", ТекущаяСтрока);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормаУстановки",ЭтотОбъект, ДопПараметры);
		ОткрытьФорму("ВнешняяОбработка.ОбновлениеРасширенийИзGithub.Форма.ФормаУстановки",Новый Структура("АдресХранилища, ИмяОбъекта",АдресХранилища,СтрокаРасширений.ИмяОбъекта),ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура Подключаемый_ПриЗакрытииВыбораФайла(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДопПараметры);
		Попытка
			ДвоичныеДанные.Записать(ВыбранныеФайлы[0]);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;  	
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Функция СкачатьРасширениеСервер(УРЛ, ДоступнаяВерсия, ТолькоРасширенияИДопОбработки = Ложь)
	
	ИсходныйURL = УРЛ;
	Массив = СтрРазделить(ИсходныйURL, "/");
	Если Массив.Количество() < 5 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Owner = Массив[3];
	Repo = Массив[4];
	
	URL_API = "/repos/" + Owner + "/" + Repo + "/releases";
	Запрос = Новый HTTPЗапрос(URL_API, ПолучитьHTTPЗаголовки(УРЛ));
	HTTPСоединение = Новый HTTPСоединение("api.github.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	Ответ = HTTPСоединение.Получить(Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда 
		МассивФайлов = Новый Массив;
		МассивРелизов = ПрочитатьJSONИзСтроки(Ответ.ПолучитьТелоКакСтроку(), Истина);
		Для Каждого Релиз Из МассивРелизов Цикл
			Если Релиз.Получить("tag_name") <> ДоступнаяВерсия Тогда
				Продолжить;
			КонецЕсли;
			Если Релиз.Получить("assets")=Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Для Каждого Файл Из Релиз.Получить("assets") Цикл
				ИмяФайла = НРег(Файл.Получить("name"));
				Если ТолькоРасширенияИДопОбработки=Ложь ИЛИ (Прав(ИмяФайла, 4) = ".cfe" ИЛИ Прав(ИмяФайла, 4) = ".epf" ИЛИ Прав(ИмяФайла, 4) = ".erf") Тогда
					СкачиваемыйФайл = СкачатьФайлГитхаб(УРЛ, Файл.Получить("browser_download_url"), ИмяФайла);
					МассивФайлов.Добавить(СкачиваемыйФайл);	
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;  
	
	Адрес = ПоместитьВоВременноеХранилище(МассивФайлов);
	
	Возврат Адрес;
	
КонецФункции 

&НаКлиенте
Процедура УстановитьРасширение(ТекущаяСтрока)  
	
	СтрокаРасширений = Расширения.НайтиПоИдентификатору(ТекущаяСтрока);
	АдресХранилища = СкачатьРасширениеСервер(СтрокаРасширений.УРЛ, СтрокаРасширений.ДоступнаяВерсия, Истина);
	
	Если АдресХранилища = Неопределено Тогда	
		Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);  
	Если МассивФайлов.Количество()=1 Тогда 
		//сразу установка 
		Для Каждого Эл Из МассивФайлов Цикл
			Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
			АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
			УстановитьРасширениеНаСервере(АдресДД, Эл.ИмяФайла, СтрокаРасширений.ИмяОбъекта);	
			ПоказатьОповещениеПользователя("Продукт обновлен. Для применения новой версии необходимо перезапустить сеанс 1С",,,,СтатусОповещенияПользователя.Важное);
		КонецЦикла;
	Иначе 
		//форма выбора установок                                                                
		ДопПараметры = Новый Структура("ТекущаяСтрока", ТекущаяСтрока);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормаУстановки",ЭтотОбъект, ДопПараметры);
		ОткрытьФорму("ВнешняяОбработка.ОбновлениеРасширенийИзGithub.Форма.ФормаУстановки",Новый Структура("АдресХранилища, ИмяОбъекта",АдресХранилища,СтрокаРасширений.ИмяОбъекта),ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли; 
	
КонецПроцедуры  

&НаКлиенте
Процедура ПослеЗакрытияФормаУстановки(Результат, Доппараметры) экспорт  
	
	Если ТипЗнч(Результат) = тип("Структура") Тогда 
		Если Результат.ПроизвелиУстановку Тогда
			СтрокаРасширений = Расширения.НайтиПоИдентификатору(Доппараметры.ТекущаяСтрока);	
			СтрокаРасширений.ТекущаяВерсия = СтрокаРасширений.ДоступнаяВерсия;
			СтрокаРасширений.ИмяОбъекта = Результат.ИмяОбъекта;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРасширениеНаСервере(АдресХранилища, ИмяФайла, ИмяОбъекта) Экспорт
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.УстановитьРасширениеНаСервере(АдресХранилища, ИмяФайла, ИмяОбъекта);
	
КонецПроцедуры   

&НаСервере		  
Функция ПрочитатьJSONИзСтроки(СтрокаВх, ПрочитатьВСоответствие = Ложь)
	
	JSON = Новый ЧтениеJSON;
	JSON.УстановитьСтроку(СтрокаВх);   
	Результат = ПрочитатьJSON(JSON, ПрочитатьВСоответствие);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьРасширениеФайла(ИмяФайла)
	
	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;
	
	Пока Истина Цикл 
		ПозицияПоследнейТочки = Найти(РасширениеФайла, "."); 
		Если ПозицияПоследнейТочки = 0 Тогда   
			Прервать; 
		Иначе   
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1)
		КонецЕсли;
	КонецЦикла;
	
	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);
	
КонецФункции  

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы ИЛИ ОбновлениеУстановщика Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительнаяОбработкаСсылка) Тогда
		СохранитьНастройкиСервер();
	Иначе 
		Сообщить("Для сохранения настроек необходимо установить эту дополнительную обработку");
	КонецЕсли;
	
КонецПроцедуры   

&НаСервере
Процедура СохранитьНастройкиСервер()
	
	СохраняемоеЗначение = Новый Структура;
	СохраняемоеЗначение.Вставить("Расширения", Расширения.Выгрузить());
	
	ДополнительнаяОбработкаОбъект = ДополнительнаяОбработкаСсылка.ПолучитьОбъект(); 
	ДополнительнаяОбработкаОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(СохраняемоеЗначение); 
	ДополнительнаяОбработкаОбъект.Записать(); 
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)           
	
	Если КлючУникальности = Неопределено Тогда
		ИмяОбработки = СтрРазделить(ЭтотОбъект.ИмяФормы, ".")[1];        
		КлючУникальности = ИмяОбработки + XMLСтрока(ТекущаяДата());        
	КонецЕсли;  
	
	Обновить(Неопределено);
	
КонецПроцедуры

