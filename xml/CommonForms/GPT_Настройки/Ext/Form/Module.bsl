
&НаКлиенте
Процедура Зарегистрироваться(Команда)
	
	ОткрытьФормуРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРегистрации()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормыРегистрации", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.GPT_Регистрация", , ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыРегистрации(Результат, Параметры) Экспорт
	
	// После регистрации обновляем данные формы с сервера, так как токен сохранился в базе
	ОбновитьДанныеССервера();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеССервера()
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	Токен = Настройки.Токен;
	АдресСервера = Настройки.АдресСервера;
	Email = Настройки.Email;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьСервер();
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СохранитьСервер()
	ПроверитьОбязательныеПлейсхолдерыПередЗаписью();
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
    НастройкиОбъект = Настройки.ПолучитьОбъект();
	НастройкиОбъект.Токен = Токен;
	НастройкиОбъект.ПромтОписание = ПромтОписание;  
	НастройкиОбъект.АдресСервера = АдресСервера;
    НастройкиОбъект.ДополнительныеСвойстваХранилище = Новый ХранилищеЗначения(ДополнительныеСвойства.ВыгрузитьЗначения());
    НастройкиОбъект.ПромтДопСвойства = ПромтДопСвойства;
    НастройкиОбъект.МодельИИ = МодельИИ;
    НастройкиОбъект.Email = Email;
	НастройкиОбъект.Записать();
КонецПроцедуры

&НаСервере
Процедура ПроверитьОбязательныеПлейсхолдерыПередЗаписью()
	
	Ошибки = Новый Массив;
	
	// Промт для описания товара
	Если НЕ ЗначениеЗаполнено(ПромтОписание) Тогда
		Ошибки.Добавить("Промт (описание) не заполнен. Обязательный плейсхолдер: {Наименование}.");
	ИначеЕсли СтрНайти(ПромтОписание, "{Наименование}") = 0 Тогда
		Ошибки.Добавить("В промте (описание) отсутствует плейсхолдер {Наименование}.");
	КонецЕсли;
	
	// Промт для доп. свойств товара
	Если НЕ ЗначениеЗаполнено(ПромтДопСвойства) Тогда
		Ошибки.Добавить("Промт (доп. свойства) не заполнен. Обязательные плейсхолдеры: {Наименование}, {Свойства}.");
	Иначе
		Если СтрНайти(ПромтДопСвойства, "{Наименование}") = 0 Тогда
			Ошибки.Добавить("В промте (доп. свойства) отсутствует плейсхолдер {Наименование}.");
		КонецЕсли;
		Если СтрНайти(ПромтДопСвойства, "{Свойства}") = 0 Тогда
			Ошибки.Добавить("В промте (доп. свойства) отсутствует плейсхолдер {Свойства}.");
		КонецЕсли;
	КонецЕсли;
	
	Если Ошибки.Количество() > 0 Тогда
		ВызватьИсключение "Настройки не сохранены." + Символы.ПС + СтрСоединить(Ошибки, Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	Токен = Настройки.Токен; 
	ПромтОписание = Настройки.ПромтОписание;
	АдресСервера = Настройки.АдресСервера;
	Email = Настройки.Email;
	МассивСвойств = Настройки.ДополнительныеСвойстваХранилище.Получить();
	Если ТипЗнч(МассивСвойств) = Тип("Массив") Тогда
		ДополнительныеСвойства.ЗагрузитьЗначения(МассивСвойств);
	КонецЕсли;
	
    ПромтДопСвойства = Настройки.ПромтДопСвойства;
	МодельИИ = Настройки.МодельИИ;
	
	Если НЕ ЗначениеЗаполнено(ПромтОписание) Тогда 
		ПромтОписание = "Создай подробное продающее описание товара ""{Наименование}"" для интернет-магазина. Описание должно быть информативным, привлекательным для покупателей и содержать ключевые характеристики.";    
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(МодельИИ) Тогда 
		МодельИИ = "auto";    
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПромтДопСвойства) Тогда 
		ПромтДопСвойства = 	"Для товара ""{Наименование}"" определи значения следующих свойств: {Свойства}. Ответ верни строго в формате JSON, где ключи — это имена свойств, а значения — их значения. Если не знаешь значение — не включай свойство в JSON.";    
	КонецЕсли; 
		
	// адрес сервера обязателен; оставляем пустым, если пользователь не заполнил
	
	Попытка
		РезультатОбновления = GPT_GitsellСервер.GitHub_ПроверитьОбновление();
		Если РезультатОбновления.Доступно Тогда
			ЕстьОбновление = Истина;
			НоваяВерсия = РезультатОбновления.НоваяВерсия;
			СсылкаНаСкачивание = РезультатОбновления.СсылкаНаСкачивание;
			Элементы.ГруппаОбновление.Видимость = Истина;
		ИначеЕсли ЗначениеЗаполнено(РезультатОбновления.Ошибка) Тогда
			// Логируем ошибку, но не прерываем открытие
			// Сообщить("Ошибка проверки обновления: " + РезультатОбновления.Ошибка);
		КонецЕсли;
	Исключение
		// Тихая обработка
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРасширение(Команда)
	
	ОчиститьСообщения();
	Статус = ОбновитьРасширениеНаСервере();
	
	Если Статус.Успех Тогда
		//@skip-check undefined-variable
		ПоказатьОповещениеПользователя("Обновление выполнено", , "Расширение обновлено до версии " + НоваяВерсия + ". Перезапустите сеанс 1С.");
		Элементы.ГруппаОбновление.Видимость = Ложь;
	Иначе
		//@skip-check undefined-variable
		ПоказатьОповещениеПользователя("Ошибка обновления", , "См. подробности в сообщениях.");
		//@skip-check undefined-variable
		ОбщегоНазначенияКлиент.СообщитьПользователю(Статус.Ошибка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкачатьРасширение(Команда)
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаСкачивание) Тогда
		//@skip-check undefined-variable
		ПоказатьОповещениеПользователя("Скачать невозможно", , "Ссылка на скачивание не определена.");
		Возврат;
	КонецЕсли;
	
	//@skip-check undefined-variable
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаНаСкачивание);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьРасширениеНаСервере()
	
	Результат = Новый Структура("Успех, Ошибка", Ложь, "");
	
	Попытка
		ДвоичныеДанные = GPT_GitsellСервер.GitHub_СкачатьФайл(СсылкаНаСкачивание);
		GPT_GitsellСервер.УстановитьРасширение(ДвоичныеДанные);
		Результат.Успех = Истина;
	Исключение
		Результат.Ошибка = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СсылкаНажатие(Элемент)
	//@skip-check undefined-variable
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Элементы.Ссылка.Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗаполнитьОписаниеНажатие(Элемент)
	ПромтОписание = "Создай подробное продающее описание товара ""{Наименование}"" для интернет-магазина. Описание должно быть информативным, привлекательным для покупателей и содержать ключевые характеристики.";
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗаполнитьДопСвойстваНажатие(Элемент)
	ПромтДопСвойства = "Для товара ""{Наименование}"" определи значения следующих свойств: {Свойства}. Ответ верни строго в формате JSON, где ключи — это имена свойств, а значения — их значения. Если не знаешь значение — не включай свойство в JSON.";
КонецПроцедуры

