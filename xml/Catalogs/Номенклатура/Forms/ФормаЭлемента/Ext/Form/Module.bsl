&НаКлиенте
Процедура GPT_ЗаполнитьОписание(Команда)  
	
	СостояниеНастроек = GPT_ОбщийВызовСервера.GPT_СостояниеНастроек();
	Если НЕ СостояниеНастроек.Готово Тогда
		ОткрытьФорму(
			"ОбщаяФорма.GPT_Регистрация",
			,
			ЭтотОбъект,
			,
			,
			,
			,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);
		Возврат;
	КонецЕсли;
	
	GPT_ОтправитьЗапросНаСервере();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучениеОтветаChatGPT", ЭтотОбъект);
	//@skip-check undefined-variable
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	//@skip-check undefined-variable
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ЭтаФорма.GPT_ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);	

КонецПроцедуры

&НаКлиенте
Процедура GPT_ОткрытьНастройки(Команда)
	ОткрытьФорму("ОбщаяФорма.GPT_Настройки", , ЭтотОбъект);
КонецПроцедуры

&НаСервере
Функция GPT_ОтправитьЗапросНаСервере()
	
	//@skip-check undefined-variable
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = Ложь; 

	//@skip-check undefined-variable
	ЭтаФорма.GPT_ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"GPT_ОбщийМодульСервер.СгенерироватьОписаниеИДопСвойстваНоменклатуры",
		Объект.Ссылка
	); 
		
КонецФункции

&НаКлиенте
Процедура ПолучениеОтветаChatGPT(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат <> Неопределено Тогда 
		Если Результат.Статус = "Ошибка" Тогда
			//@skip-check undefined-variable
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
		ИначеЕсли Результат.Статус = "Выполнено" Тогда  	
			ОтветИИ = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
			
			ОписаниеИИ = "";
			Если ОтветИИ <> Неопределено И ОтветИИ.Свойство("Описание") Тогда
				ОписаниеИИ = ОтветИИ.Описание;
			КонецЕсли;
			
			Если GPT_ОбщийВызовСервера.ЭтоУНФ() ИЛИ GPT_ОбщийВызовСервера.ЭтоРозница() Тогда
				//@skip-check undefined-variable
				Объект.Комментарий = ОписаниеИИ;
			Иначе 
				//@skip-check undefined-variable
				Объект.Описание = ОписаниеИИ;           
			КонецЕсли;
			
			Если ОтветИИ <> Неопределено И ОтветИИ.Свойство("ДопСвойства") Тогда
				Если ТипЗнч(ОтветИИ.ДопСвойства) = Тип("Соответствие") ИЛИ ТипЗнч(ОтветИИ.ДопСвойства) = Тип("Структура") Тогда
					Если ОтветИИ.ДопСвойства.Количество() > 0 Тогда
						ЗаполнитьДополнительныеСвойстваНаФорме(ОтветИИ.ДопСвойства);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
							
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеСвойстваНаФорме(Соответствие)
	
	Для Каждого Эл Из Соответствие Цикл
		Если НЕ ЗначениеЗаполнено(GPT_ОбщийМодульСервер.ПолучитьЗначениеДополнительногоРеквизитаНаФорме(ЭтотОбъект,Эл.Ключ)) Тогда 
			GPT_ОбщийМодульСервер.УстановитьЗначениеДополнительногоРеквизитаНаФорме(ЭтотОбъект,Эл.Ключ,Эл.Значение);
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

