Процедура GPT_ПриСозданииНаСервере(Форма, ПараметрыРазмещения=Неопределено) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Элементы = Форма.Элементы;
	
	Если ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда 
		
		Если Элементы.Найти("GPT_ЗаполнитьОписание")<>Неопределено Тогда 
			Возврат;
		КонецЕсли;   
		
		ДобавляемыеРеквизиты	= Новый Массив;		
		GPT_ДлительнаяОперация = Новый РеквизитФормы("GPT_ДлительнаяОперация", Новый ОписаниеТипов());
		ДобавляемыеРеквизиты.Добавить(GPT_ДлительнаяОперация);
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты); 
		
		Если ЭтоУНФ() ИЛИ ЭтоРозница() Тогда 
			Если Элементы.Найти("ОсновныеРеквизиты")<>Неопределено Тогда 
				РодительГруппы = Элементы.ОсновныеРеквизиты;
			Иначе 
				РодительГруппы = Элементы.ГруппаЕдиницыИзмеренияИАртикулПравая;
			КонецЕсли;
			
			ПередЭлементом = Элементы.Комментарий;
		Иначе 
			РодительГруппы = Элементы.ГруппаОписания;
			ПередЭлементом = Элементы.Описание;
		КонецЕсли;
		
		// Группа для "Заполнить ИИ" + кнопка настроек в одной строке
		ГруппаИИ = Форма.Элементы.Вставить("GPT_ГруппаИИ", Тип("ГруппаФормы"), РодительГруппы, ПередЭлементом);
		ГруппаИИ.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаИИ.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		Декорация = Форма.Элементы.Вставить("GPT_ЗаполнитьОписание", Тип("ДекорацияФормы"), ГруппаИИ, Неопределено);
		
		Декорация.Вид = ВидДекорацииФормы.Надпись;
		Декорация.Гиперссылка = Истина;
		// Важно: не растягиваем на всю ширину, иначе второй элемент уезжает вниз
		Декорация.РастягиватьПоГоризонтали = Ложь;
		Декорация.Заголовок = "Заполнить ИИ";
		Декорация.УстановитьДействие("Нажатие", "GPT_ЗаполнитьОписание");
		
		ДекорацияНастройки = Форма.Элементы.Вставить("GPT_ОткрытьНастройки", Тип("ДекорацияФормы"), ГруппаИИ, Неопределено);
		ДекорацияНастройки.Вид = ВидДекорацииФормы.Надпись;
		ДекорацияНастройки.Гиперссылка = Истина;
		ДекорацияНастройки.РастягиватьПоГоризонтали = Ложь;
		ДекорацияНастройки.Заголовок = "⚙";
		ДекорацияНастройки.УстановитьДействие("Нажатие", "GPT_ОткрытьНастройки");
		
	КонецЕсли;
	
		
КонецПроцедуры   

Функция ОтправитьЗапрос(Промт) Экспорт
	
	СтрокаJSON = СериализоватьЗапросВJSON(Промт);

	Соединение = ПолучитьHTTPСоединение();   
	Запрос = Новый HTTPЗапрос(ПолучитьПутьChatCompletions(), ПолучитьHTTPЗаголовки());  
	
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
	Результат = Соединение.ОтправитьДляОбработки(Запрос); //POST
		
	Если Результат.КодСостояния=200 Тогда 
		ЧтениеJson = Новый ЧтениеJson;
		ЧтениеJson.УстановитьСтроку(Результат.ПолучитьТелоКакСтроку());
		РезСтруктура = ПрочитатьJSON(ЧтениеJson);
		
		МассивРезультатов = РезСтруктура["choices"];			
		Если МассивРезультатов <> Неопределено 
			И МассивРезультатов.Количество() 
			И ТипЗнч(МассивРезультатов[0]) = Тип("Структура")
			И МассивРезультатов[0]["message"] <> Неопределено
			И ТипЗнч(МассивРезультатов[0]["message"]) = Тип("Структура") Тогда
			Ответ = СокрЛП(МассивРезультатов[0]["message"]["content"]);	
		Конецесли;

	Иначе 
		СообщениеобОшибке = "Не удалось получить ответ на сайте. Код ошибки: "+Результат.КодСостояния +" "+ Результат.ПолучитьТелоКакСтроку();  
		ВызватьИсключение СообщениеобОшибке;
	КонецЕсли; 
	
	Возврат Ответ;
	
КонецФункции  

Функция СериализоватьЗапросВJSON(ТекстЗапроса)	
	
	Настройки = НастройкаПоУмолчанию();
	
	Сообщения = Новый Массив;
	Сообщение = Новый Структура;
	Сообщение.Вставить("role", "user"); 
	Сообщение.Вставить("content", ТекстЗапроса); 
	Сообщения.Добавить(Сообщение); 
	
	Структура = Новый Структура;
	Модель = Настройки.МодельИИ;
	Если НЕ ЗначениеЗаполнено(Модель) Тогда
		Модель = "auto";
	КонецЕсли;
	Структура.Вставить("model", Модель);
	Структура.Вставить("messages", Сообщения);
	
			
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,Структура);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции  

Функция ПолучитьСтруктуруURIСервера()
	
	Настройки = НастройкаПоУмолчанию();
	
	Если НЕ ЗначениеЗаполнено(Настройки.АдресСервера) Тогда
		ВызватьИсключение "Не заполнен адрес сервера в настройках GPT.";
	КонецЕсли;
	
	//@skip-check undefined-variable
	Возврат ОбщегоНазначенияКлиентСервер.СтруктураURI(Настройки.АдресСервера);
	
КонецФункции

Функция НормализоватьПутьНаСервере(ПутьНаСервере)
	
	Если НЕ ЗначениеЗаполнено(ПутьНаСервере) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Лев(ПутьНаСервере, 1) <> "/" Тогда
		ПутьНаСервере = "/" + ПутьНаСервере;
	КонецЕсли;
	
	Пока СтрДлина(ПутьНаСервере) > 1 И Прав(ПутьНаСервере, 1) = "/" Цикл
		ПутьНаСервере = Лев(ПутьНаСервере, СтрДлина(ПутьНаСервере) - 1);
	КонецЦикла;
	
	Возврат ПутьНаСервере;
	
КонецФункции

Функция ПолучитьПутьChatCompletions()
	
	//@skip-check undefined-variable
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат "/proxy/openai/v1/chat/completions";
	КонецЕсли;
	
	СтруктураURI = ПолучитьСтруктуруURIСервера();
	Путь = НормализоватьПутьНаСервере(СтруктураURI.ПутьНаСервере);
	
	Если НЕ ЗначениеЗаполнено(Путь) Тогда
		Возврат "/v1/chat/completions";
	КонецЕсли;
	
	Если Прав(Путь, СтрДлина("/v1/chat/completions")) = "/v1/chat/completions" Тогда
		Возврат Путь;
	КонецЕсли;
	
	Если Прав(Путь, 3) = "/v1" Тогда
		Возврат Путь + "/chat/completions";
	КонецЕсли;
	
	Возврат Путь + "/v1/chat/completions";
	
КонецФункции


Функция ПолучитьHTTPСоединение() 
	
	//@skip-check undefined-variable
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат Новый HTTPСоединение("mangelaav-oai-proxy.hf.space",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	КонецЕсли;
	
	СтруктураURI = ПолучитьСтруктуруURIСервера();
	
	Схема = "";
	Если СтруктураURI.Свойство("Схема") Тогда
		Схема = НРег(СтруктураURI.Схема);
	КонецЕсли;
	
	Порт = 0;
	Если СтруктураURI.Свойство("Порт") Тогда
		Порт = СтруктураURI.Порт;
	КонецЕсли;
	
	Если Порт = 0 Тогда
		Порт = ?(Схема = "https", 443, 80);
	КонецЕсли;
	
	Если Схема = "http" Тогда
		Возврат Новый HTTPСоединение(СтруктураURI.Хост, Порт);
	КонецЕсли;
	
	Возврат Новый HTTPСоединение(СтруктураURI.Хост, Порт,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	
КонецФункции

Функция ПолучитьHTTPЗаголовки() 
	
	Настройки = GPT_ОбщийМодульСервер.НастройкаПоУмолчанию();
	
	Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
    Заголовки.Вставить("Cache-Control", "no-cache");
   	Заголовки.Вставить("Accept-Encoding", "identity");
	Заголовки.Вставить("X-Gitsell-Repo", "msrv-tech/1c-ai-autofill");
	
	Токен = ПолучитьТокен();
	Если ЗначениеЗаполнено(Токен) Тогда
		Заголовки.Вставить("Authorization", "Bearer " + Токен);
	КонецЕсли;
	
	Возврат Заголовки;      
	
КонецФункции   

Функция ПолучитьТокен()   
	
	Результат = "";	
	Настройки = НастройкаПоУмолчанию(); 
	Если ЗначениеЗаполнено(Настройки.Токен) Тогда 
		Результат = Настройки.Токен;
	КонецЕсли;
	
	//@skip-check undefined-variable
	Если НЕ ЗначениеЗаполнено(Результат) И ОбщегоНазначения.РазделениеВключено() Тогда 
		//для фреш единый ключ  
		
	КонецЕсли;
	
	Возврат Результат;    
	
КонецФункции 

Функция НастройкаПоУмолчанию() Экспорт   
	ВыборкаНастроек = Справочники.GPT_Настройка.Выбрать();
	Если ВыборкаНастроек.Следующий() Тогда
		Возврат ВыборкаНастроек.Ссылка;
	Иначе 
		НовыеНастройки = Справочники.GPT_Настройка.СоздатьЭлемент();
		НовыеНастройки.Записать();
		Возврат НовыеНастройки.Ссылка;
	КонецЕсли;
КонецФункции

// Автоматическое заполнение дополнительных свойств номенклатуры через ИИ
Функция АвтоЗаполнитьДопСвойстваНоменклатуры(ОбъектНоменклатуры) Экспорт

    Настройки = НастройкаПоУмолчанию();
    СписокСвойств = Настройки.ДополнительныеСвойстваХранилище.Получить();
    Шаблон = Настройки.ПромтДопСвойства;
    Если НЕ ТипЗнч(СписокСвойств)=Тип("Массив") Или НЕ ЗначениеЗаполнено(Шаблон) Тогда
        Возврат Неопределено;
    КонецЕсли;

	СписокИмен = Новый Массив;
	Для Каждого ЗаполняемоеСвойство Из СписокСвойств Цикл
		Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ОбъектНоменклатуры, ЗаполняемоеСвойство) Тогда
			СписокИмен.Добавить(ЗаполняемоеСвойство.Наименование);
		КонецЕсли;
	КонецЦикла;
	
    // Формируем промт
    Наименование = ОбъектНоменклатуры.Наименование;
    СтрокаСвойств = СтрСоединить(СписокИмен, ", ");
    Промт = СтрЗаменить(Шаблон, "{Наименование}", Наименование);
    Промт = СтрЗаменить(Промт, "{Свойства}", СтрокаСвойств);

    // Отправляем запрос к ИИ
    Ответ = ОтправитьЗапрос(Промт);
    Если НЕ ЗначениеЗаполнено(Ответ) Тогда
        Возврат Неопределено;
    КонецЕсли;

    // Ожидаем JSON
    Попытка
        Соответствие = ПолучитьСоответствиеИзJSON(Ответ);
	Исключение
		ВызватьИсключение "Непредвиденный символ при чтении JSON: " + ОписаниеОшибки() + Символы.ПС + "Ответ ИИ: " + Ответ;
    КонецПопытки;
    
    Возврат Соответствие;

КонецФункции

// Единый запрос: описание + доп. свойства номенклатуры за 1 вызов к ИИ
Функция СгенерироватьОписаниеИДопСвойстваНоменклатуры(СсылкаНоменклатуры) Экспорт
	
	Если СсылкаНоменклатуры = Неопределено Тогда
		ВызватьИсключение "Не передана ссылка номенклатуры";
	КонецЕсли;
	
	ОбъектНоменклатуры = СсылкаНоменклатуры.ПолучитьОбъект();
	Настройки = НастройкаПоУмолчанию();

	Наименование = ОбъектНоменклатуры.Наименование;
	
	ПромтОписание = Настройки.ПромтОписание;
	Если ЗначениеЗаполнено(ПромтОписание) Тогда
		ПромтОписание = СтрЗаменить(ПромтОписание, "{Наименование}", Наименование);
	КонецЕсли;
	
	ДопСвойстваНужны = Ложь;
	СтрокаСвойств = "";
	ПромтДопСвойства = "";
	
	Если ЗначениеЗаполнено(Настройки.ПромтДопСвойства) 
		И ЗначениеЗаполнено(Настройки.ДополнительныеСвойстваХранилище) Тогда
		
		СписокСвойств = Настройки.ДополнительныеСвойстваХранилище.Получить();
		Если ТипЗнч(СписокСвойств) = Тип("Массив") Тогда
			
			СписокИмен = Новый Массив;
			Для Каждого ЗаполняемоеСвойство Из СписокСвойств Цикл
				Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ОбъектНоменклатуры, ЗаполняемоеСвойство) Тогда
					СписокИмен.Добавить(ЗаполняемоеСвойство.Наименование);
				КонецЕсли;
			КонецЦикла;
			
			Если СписокИмен.Количество() > 0 Тогда
				ДопСвойстваНужны = Истина;
				СтрокаСвойств = СтрСоединить(СписокИмен, ", ");
				ПромтДопСвойства = СтрЗаменить(Настройки.ПромтДопСвойства, "{Наименование}", Наименование);
				ПромтДопСвойства = СтрЗаменить(ПромтДопСвойства, "{Свойства}", СтрокаСвойств);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Промт = 
		"Верни результат строго в JSON (без Markdown и без пояснений)." + Символы.ПС +
		"Структура:" + Символы.ПС +
		"{" + Символы.ПС +
		"  ""Описание"": ""<строка>""," + Символы.ПС +
		"  ""ДопСвойства"": { ""<Имя свойства>"": ""<значение>"", ... }" + Символы.ПС +
		"}" + Символы.ПС +
		"Поле ""ДопСвойства"" может быть пустым объектом {}." + Символы.ПС + Символы.ПС +
		"Товар: """ + Наименование + """" + Символы.ПС +
		"Описание: " + ПромтОписание + Символы.ПС;
	
	Если ДопСвойстваНужны Тогда
		Промт = Промт + 
			Символы.ПС +
			"ДопСвойства (ключи строго из списка: " + СтрокаСвойств + "):" + Символы.ПС +
			ПромтДопСвойства + Символы.ПС;
	Иначе
		Промт = Промт + Символы.ПС + "ДопСвойства не заполняй. Верни ""ДопСвойства"": {}." + Символы.ПС;
	КонецЕсли;
	
	Ответ = ОтправитьЗапрос(Промт);
	Если НЕ ЗначениеЗаполнено(Ответ) Тогда
		ВызватьИсключение "Пустой ответ от ИИ";
	КонецЕсли;
	
	Попытка
		Данные = ПолучитьСтруктуруИзJSON(Ответ);
	Исключение
		ВызватьИсключение "Непредвиденный символ при чтении JSON: " + ОписаниеОшибки() + Символы.ПС + "Ответ ИИ: " + Ответ;
	КонецПопытки;
	
	Если Данные = Неопределено Или ТипЗнч(Данные) <> Тип("Структура") Тогда
		ВызватьИсключение "Ответ ИИ не является JSON-объектом. Ответ ИИ: " + Ответ;
	КонецЕсли;
	
	Если НЕ Данные.Свойство("Описание") Тогда
		ВызватьИсключение "В ответе ИИ нет поля ""Описание"". Ответ ИИ: " + Ответ;
	КонецЕсли;
	
	Описание = Данные.Описание;
	
	ДопСвойства = Новый Структура();
	Если ДопСвойстваНужны Тогда
		Если Данные.Свойство("ДопСвойства") Тогда
			ДопСвойства = Данные.ДопСвойства;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Описание,ДопСвойства", Описание, ДопСвойства);
	
КонецФункции

Функция ПолучитьСоответствиеИзJSON(СтрокаJSON)
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Соответствие = ПрочитатьJSON(ЧтениеJSON,Истина);
	ЧтениеJSON.Закрыть();
	
	Возврат Соответствие;
КонецФункции

Функция ПолучитьСтруктуруИзJSON(СтрокаJSON)
	
	СтрокаJSON = ПодготовитьJSONИзОтветаИИ(СтрокаJSON);
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Структура = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Структура;
КонецФункции

Функция ПодготовитьJSONИзОтветаИИ(ТекстОтвета)
	
	Если ТекстОтвета = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = СокрЛП(ТекстОтвета);
	
	// Частый кейс: ответ обёрнут в ```json ... ```
	Если Лев(Текст, 3) = "```" Тогда
		ПозицияПС = СтрНайти(Текст, Символы.ПС);
		Если ПозицияПС > 0 Тогда
			Текст = Сред(Текст, ПозицияПС + СтрДлина(Символы.ПС));
		КонецЕсли;
		
		Текст = СокрЛП(Текст);
		
		ПозицияЗакрытия = СтрНайти(Текст, "```");
		Если ПозицияЗакрытия > 0 Тогда
			Текст = Лев(Текст, ПозицияЗакрытия - 1);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СокрЛП(Текст);
	
КонецФункции
	
Функция ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита) Экспорт 
	
	Список = Форма.Свойства_ОписаниеДополнительныхРеквизитов;
	
	Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяРеквизита);
	
	Если ЗначениеЗаполнено(Свойство) Тогда
	    НайденныеСтроки = Список.НайтиСтроки(Новый Структура("Свойство", Свойство));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0];		
		КонецЕсли; 		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьЗначениеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита) Экспорт
	Перем ЗначениеРеквизита;
	
	ПолеДополнительногоРеквизитаНаФорме = ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита);
	
	Если не ПолеДополнительногоРеквизитаНаФорме = Неопределено Тогда
		Возврат Форма[ПолеДополнительногоРеквизитаНаФорме.ИмяРеквизитаЗначение];
	КонецЕсли;
	
КонецФункции

Процедура УстановитьЗначениеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита, ЗначениеРеквизита) Экспорт
	
	ПолеДополнительногоРеквизитаНаФорме = ПолучитьПолеДополнительногоРеквизитаНаФорме(Форма, ИмяРеквизита);
	
	Если не ПолеДополнительногоРеквизитаНаФорме = Неопределено Тогда
		Форма[ПолеДополнительногоРеквизитаНаФорме.ИмяРеквизитаЗначение] = ЗначениеРеквизита;
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоУНФ() Экспорт
	
	ИмяКонфигурации = Метаданные.Имя;
	Если ИмяКонфигурации = "УправлениеНебольшойФирмой" Тогда 
		Возврат Истина;
	иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции   

Функция ЭтоРозница() Экспорт
	
	ИмяКонфигурации = Метаданные.Имя;
	Если ИмяКонфигурации = "Розница" Тогда 
		Возврат Истина;
	иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
